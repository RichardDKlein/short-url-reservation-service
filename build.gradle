/**
 * The Short URL Reservation Service
 * (Copyright 2024 by Richard Klein)
 */

apply plugin: 'java'

repositories {
  mavenLocal()
  mavenCentral()
  maven {url "https://repo.spring.io/milestone"}
  maven {url "https://repo.spring.io/snapshot"}
}

dependencies {
  implementation (
          'org.springframework.boot:spring-boot-starter-web:3.2.1',
          'com.amazonaws.serverless:aws-serverless-java-container-springboot3:[2.0-SNAPSHOT,)',
          'software.amazon.awssdk:dynamodb-enhanced:2.22.13',
          'software.amazon.awssdk:ssm:2.22.13'
  )

  testImplementation("com.amazonaws.serverless:aws-serverless-java-container-core:[2.0-SNAPSHOT,):tests")
  testImplementation("org.apache.httpcomponents.client5:httpclient5:5.2.1")
  testImplementation(platform("org.junit:junit-bom:5.9.3"))
  testImplementation("org.junit.jupiter:junit-jupiter")
}

// Add the "-parameters" compilation switch, so that @PathVariable works.
tasks.withType(JavaCompile) {
  options.compilerArgs << "-parameters"
}

task buildZip(type: Zip) {
  from compileJava
  from processResources
  into('lib') {
    from(configurations.compileClasspath) {
      exclude 'tomcat-embed-*'
    }
  }
}

test {
  useJUnitPlatform()
}

build.dependsOn buildZip
